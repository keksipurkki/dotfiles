" vim: nowrap fdm=marker
"
" File:         vimrc
" Last Change:  2019-10-12

let &packpath.=','.expand('<sfile>:h')

syntax on
filetype plugin indent on

" Settings {{{1

" Note: No settings related to linewidth/shiftwidth. They are defined in .editorconfig

set shellcmdflag+=l " Read ~/.profile

let mapleader = ";"
let maplocalleader = ";"
set completeopt=longest,menuone
set guicursor+=a:blinkon0
set guifont=Monego:h14 " Monaco with bold/italic font weights

set hidden
set autoread
set directory=/tmp
set guioptions-=T
set ignorecase
set incsearch
set modeline
set nobackup
set nowritebackup
set number
set scrolloff=8
set cmdheight=1
set updatetime=300
set background=light
set laststatus=2
set shortmess+=c
set signcolumn=yes
set smartcase
set et
set t_Co=256
set vb t_vb=

" Jump to the line that was last edited
function! LastLine()
  if line("'\"") > 1 && line("'\"") <= line("$") |
    exe "normal! g`\"" |
  endif
endfunction

function! SyntaxEscapeHatch() 
  if getfsize(expand("%")) > 1024 * 1024 | syntax off | endif
endfunction

autocmd BufReadPre * silent call SyntaxEscapeHatch()
autocmd BufReadPost * silent call LastLine()
autocmd FileType yaml setlocal ts=2 sts=2 sw=2 expandtab
autocmd FileType markdown setlocal spell
autocmd FileType txt setlocal spell
autocmd BufRead,BufNewFile *.cwiki setlocal ft=confluencewiki

" Indent
function! IndentReset()
  set autoindent
  set smartindent
  set nocindent
endfunction

autocmd BufReadPost * silent call IndentReset()

" Git
command! GitRoot :execute 'lcd' systemlist('git rev-parse --show-toplevel')[0]
command! -nargs=* GitLoadEm :args `set -f ; printf \"\%s\\n\" \"$(git grep --cached -Il '' )\"`

" Mappings {{{1
noremap + "+
noremap TT :tabe<CR>
noremap Y y$
noremap D d$
noremap <C-L> :redraw!<CR>
map <F10> :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<'
\ . synIDattr(synID(line("."),col("."),0),"name") . "> lo<"
\ . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>

cabbrev <expr> E ((getcmdtype() == ':' && getcmdpos() <= 2)? 'e' : 'E')
cabbrev <expr> W ((getcmdtype() == ':' && getcmdpos() <= 2)? 'w' : 'W')
cabbrev <expr> Q ((getcmdtype() == ':' && getcmdpos() <= 2)? 'q' : 'Q')
cabbrev res Res

command! -range=% CRLFify :e ++ff=dos
command! DosToUnix :e ++ff=dos | :setlocal ff=unix | :w
command! Vimrc :e $MYVIMRC
command! TakeMeHere :lcd %:p:h
command! -range=% ValidateJSON <line1>,<line2>:!python -mjson.tool
command! -range=% ValidateXML <line1>,<line2>:!tidy -qi -xml
command! -range=% StripSpaces <line1>,<line2>:s/\s\+$//g|w
command! -range=% Indent <line1>,<line2>:!indent

" Prettier
function! CSVFormat()
  %substitute/\t/;/g
  %substitute/,/./g
  %substitute///g
  global/^\s*$/d
endfunction

command! -range=% PrettierHtml <line1>,<line2>:!tidy -qi -utf8 || true
command! -range=% PrettierSwift <line1>,<line2>:!swift format
command! -range=% PrettierCSV :call CSVFormat()

" Filetypes {{{1
let g:is_bash=1
let g:tex_flavor = 'latex'
let g:tex_comment_nospell= 1
let g:tex_nine_config = {
      \'compiler' : 'latex',
      \'viewer': {'app': 'open', 'target': 'pdf'},
      \}

let netrw_browsex_viewer = "open"

" Vimdiff
if &diff
  noremap <buffer> <C-F> ]c
  noremap <buffer> <C-B> [c
  noremap <buffer> dp zRdp
  noremap <buffer> do zRdo
endif

" Spell checking
noremap <D-s> ]s
noremap <D-A-s> [s

"Colors {{{1
let g:airline_powerline_fonts=0
function! ColorFixes()

  hi Conceal  guifg=#000000   guibg=#ffffff  gui=none ctermfg=16 ctermbg=231 cterm=none
  hi LineNr guibg=white guifg=darkgrey
  hi SignColumn guibg=white
  hi CocErrorSign    guibg=white guifg=#ff0000
  hi CocWarningSign  guibg=white guifg=#ff922b
  hi CocInfoSign     guibg=white guifg=#fab005
  hi CocHintSign     guibg=white guifg=#15aabf
  hi CocErrorHighlight gui=underline guisp=#ff0000

  " Simplify the color scheme
  hi Function guifg=#000000
  hi Comment guifg=#cccccc gui=bold
  hi Identifier guifg=#000000

endfunction
autocmd BufReadPost * silent call ColorFixes()

if !has('gui_running')
  colorscheme terminal
endif

" JavaScript {{{1
let g:javascript_plugin_flow = 0
let g:vim_jsx_pretty_highlight_close_tag = 0

" Override default functionalities with CoC/LSPs {{{1
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gy <Plug>(coc-type-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-references)
nmap <leader>rn <Plug>(coc-rename)
nmap <silent> <2-LeftMouse> :silent call CocActionAsync('highlight')<cr>
